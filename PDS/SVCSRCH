/* Rexx! Here boy! Good Rexx!

   SVCSRCH accepts 0,1,2 parameters. The default display is of USER SVCs
    (200-255). Example parameters -> '109 122' or '100'
   When Extended Service Router (ESR) SVCs are encountered, they are
    also displayed
   Empty SVCTABLE/ESRTABLE entries are detected and *not* displayed

*/
Signal OFF NOVALUE
Select
  When Arg() = 0 then
    Do
       SVCNUMBER_First = 200
       SVCNUMBER_Last = 255
       End
  When Arg() = 1 then
    Do
       Parse Arg SVCNUMBER_First SVCNUMBER_Last .
       If SVCNUMBER_Last = '' then
         SVCNUMBER_Last = 255
       End
  End
Say 'You asked to begin searching at SVC'SVCNUMBER_First,
    'and end at SVC'SVCNUMBER_Last
PRTDSVAR = Copies('_',79)
Call PRTLmput
PRTDSVAR = Left(' Searching for SVC information...',79)
Call PRTLmput
SVCTABLE = SVC000()
Do until SVCNUMBER_First > SVCNUMBER_Last
  SVCHEADER = C2x(Storage(C2x(Storage(SVCTABLE,4)),80))
  If Substr(SVCHEADER,1,8) \= '1F22BF27' then
    Do
      PRTDSVAR = 'SVC' || Right(SVCNUMBER_First,3,'0') '@',
      C2x(Storage(SVCTABLE,4)) ':'
      Call PRTLmput
      Call PRThexdump
      End
  If C2d(Bitand(Substr(Storage(SVCTABLE,5),5),'04'x)) = 4 then
    Do
      ESRTABLE = C2x(Storage(SVCTABLE,4))
      ESRCOUNT = C2d(Storage(X2X(ESRTABLE, '04'),4))
      ESRTABLE = X2X(ESRTABLE, '08')
      Do ESR_index = 0 to ESRCOUNT by 1
        If C2x(Storage(C2x(Storage(ESRTABLE,4)),4)) \= '4110016D' then
          Do
            PRTDSVAR = 'ESR entry' Right(ESR_index,3,'0') '@',
                       C2x(Storage(ESRTABLE,4)) ':'
            Call PRTLmput
            SVCHEADER = C2x(Storage(C2x(Storage(ESRTABLE,4)),80))
            Call PRThexdump
            End
        ESRTABLE = X2X(ESRTABLE, '08')
        End
      End
  SVCTABLE = X2X(SVCTABLE, '08')
  SVCNUMBER_First = SVCNUMBER_First + 1
  End
PRTDSVAR = Left(' Searching for SVC information... done',79)
Call PRTLmput
PRTDSVAR = Copies('_',79)
Call PRTLmput
Exit

CVT:      Procedure; Return C2x(Storage(X2X('10', '00'),4))
IOSDSHID: Procedure; Return C2x(Storage(X2X(CVT(),'42C'),4))
SCVT:     Procedure; Return C2x(Storage(X2X(CVT(), 'C8'),4))
SVCT:     Procedure; Return C2x(Storage(X2X(SCVT(), '84'),4))
SVC000:   Procedure expose SVCNUMBER_First
                     Return X2X(SVCT(), D2x(SVCNUMBER_First * 8))
X2X:      Procedure; Return D2x(X2d(Arg(1)) + X2d(Arg(2)))

PRTLmput:
 Say PRTDSVAR
 Return

PRThexdump:
 OFFSET = 1
 Do 5
    Parse var SVCHEADER =(OFFSET) HEX1 +8 HEX2 +8 HEX3 +8 HEX4 +8 .
    PRTDSVAR = HEX1'  'HEX2'  'HEX3'  'HEX4'  |',
               X2c(Substr(SVCHEADER, OFFSET, 32)) '|'
    Call PRTLmput
    OFFSET = OFFSET + 32
    End
 Return
