/* REXX */
 /********************************************************************/
 /*                                                                  */
 /*       NAME: IGGCSIRX                                             */
 /*DESCRIPTION: THIS REXX EXEC IS USED TO CALL THE CATALOG           */
 /*             SEARCH INTERFACE.                                    */
 /*      INPUT: FILTER KEY                                           */
 /*     OUTPUT: DATA SETS NAMES AND VOLUMES                          */
 /*                                                                  */
 /********************************************************************/
 SAY 'Enter filter key'             /*   ASK FOR FILTER KEY          */
 PULL KEY                           /*   GET FILTER KEY              */
 /********************************************************************/
 /*                                                                  */
 /*  INITIALIZE THE PARM LIST                                        */
 /*                                                                  */
 /********************************************************************/
MODRSNRC = SUBSTR(' ',1,4)          /*   CLEAR MODULE/RETURN/REASON  */
CSIFILTK = SUBSTR(KEY,1,44)         /*   MOVE FILTER KEY INTO LIST   */
CSICATNM = SUBSTR(' ',1,44)         /*   CLEAR CATALOG NAME          */
CSIRESNM = SUBSTR(' ',1,44)         /*   CLEAR RESUME NAME           */
CSIDTYPS = SUBSTR(' ',1,16)         /*   CLEAR ENTRY TYPES           */
CSICLDI  = SUBSTR('Y',1,1)          /*   INDICATE DATA AND INDEX     */
CSIRESUM = SUBSTR(' ',1,1)          /*   CLEAR RESUME FLAG           */
CSIS1CAT = SUBSTR(' ',1,1)          /*   INDICATE SEARCH > 1 CATALOGS*/
CSIRESRV = SUBSTR(' ',1,1)          /*   CLEAR RESERVE CHARACTER     */
CSINUMEN = '0001'X                  /*   INIT NUMBER OF FIELDS       */
CSIFLD1    = SUBSTR('VOLSER',1,8)   /*   INIT FIELD 1 FOR VOLSERS    */
 /********************************************************************/
 /*                                                                  */
 /*  BUILD THE SELECTION CRITERIA FIELDS PART OF PARAMETER LIST      */
 /*                                                                  */
 /********************************************************************/
CSIOPTS  = CSICLDI || CSIRESUM || CSIS1CAT || CSIRESRV
CSIFIELD = CSIFILTK || CSICATNM || CSIRESNM || CSIDTYPS || CSIOPTS
CSIFIELD = CSIFIELD || CSINUMEN || CSIFLD1

 /********************************************************************/
 /*                                                                  */
 /*  INITIALIZE AND BUILD WORK ARE OUTPUT PART OF PARAMETER LIST     */
 /*                                                                  */
 /********************************************************************/
WORKLEN = 2048
DWORK = '00000800'X || COPIES('00'X,WORKLEN-4)

 /********************************************************************/
 /*                                                                  */
 /*  INITIALIZE WORK VARIABLES                                       */
 /*                                                                  */
 /********************************************************************/
RESUME = 'Y'
CATNAMET = SUBSTR(' ',1,44)
DNAMET = SUBSTR(' ',1,44)

 /********************************************************************/
 /*                                                                  */
 /*  SET UP LOOP FOR RESUME (IF A RESUME IS NCESSARY)                */
 /*                                                                  */
 /********************************************************************/
DO WHILE RESUME = 'Y'

 /********************************************************************/
 /*                                                                  */
 /*  ISSUE LINK TO CATALOG GENERIC FILTER INTERFACE                  */
 /*                                                                  */
 /********************************************************************/
 ADDRESS LINKPGM 'IGGCSI00  MODRSNRC  CSIFIELD  DWORK'

 RESUME = SUBSTR(CSIFIELD,150,1)    /* GET RESUME FLAG FOR NEXT LOOP */
 USEDLEN = C2D(SUBSTR(DWORK,9,4))   /* GET AMOUNT OF WORK AREA USED  */
 POS1=15                            /* STARTING POSITION             */
 /********************************************************************/
 /*                                                                  */
 /*  PROCESS DATA RETURNED IN WORK AREA                              */
 /*                                                                  */
 /********************************************************************/
 DO WHILE POS1 < USEDLEN            /* DO UNTIL ALL DATA IS PROCESSED*/
  Select
   When SUBSTR(DWORK,POS1+1,1) = '0' then /* IF CATALOG, PRINT CATALOG HEAD*/
     DO
     CATNAME=SUBSTR(DWORK,POS1+2,44)
     CATCODE=C2X(SUBSTR(DWORK,POS1,1))
     CATRC=C2D(SUBSTR(DWORK,POS1+49,1))
     If CATRC = 0 then CATRN = 0
      Else CATRN=C2D(SUBSTR(DWORK,POS1+50,1))
     IF CATNAME \= CATNAMET THEN /* IF RESUME NAME MAY ALREADY BE*/
      DO                         /*    PRINTED                   */
       SAY 'Catalog' Strip(CATNAME) 'of code type' CATCODE 'with RC and RSN of'
       SAY ' '
       CATNAMET = CATNAME
      END
     POS1 = POS1 + 50
     END
   Otherwise  /* Process the catalogue entry */
    Do
     DNAME = SUBSTR(DWORK,POS1+2,44)  /* GET ENTRY NAME                */

 /********************************************************************/
 /*                                                                  */
 /*  ASSIGN ENTRY TYPE NAME                                          */
 /*                                                                  */
 /********************************************************************/
   Select
    When SUBSTR(DWORK,POS1+1,1) = 'C' THEN DTYPE = 'CLUSTER '
    When SUBSTR(DWORK,POS1+1,1) = 'D' THEN DTYPE = 'DATA    '
    When SUBSTR(DWORK,POS1+1,1) = 'I' THEN DTYPE = 'INDEX   '
    When SUBSTR(DWORK,POS1+1,1) = 'A' THEN DTYPE = 'NONVSAM '
    When SUBSTR(DWORK,POS1+1,1) = 'H' THEN DTYPE = 'GDS     '
    When SUBSTR(DWORK,POS1+1,1) = 'B' THEN DTYPE = 'GDG     '
    When SUBSTR(DWORK,POS1+1,1) = 'R' THEN DTYPE = 'PATH    '
    When SUBSTR(DWORK,POS1+1,1) = 'G' THEN DTYPE = 'AIX     '
    When SUBSTR(DWORK,POS1+1,1) = 'X' THEN DTYPE = 'ALIAS   '
    When SUBSTR(DWORK,POS1+1,1) = 'U' THEN DTYPE = 'UCAT    '
    Otherwise DTYPE = '        '
    End
 /********************************************************************/
 /*                                                                  */
 /*  HAVE NAME AND TYPE, GET VOLSER INFO                             */
 /*                                                                  */
 /********************************************************************/
    POS1 = POS1 + 46
    NUMVOL = C2D(SUBSTR(DWORK,POS1+4,2))/6 /* HOW MANY VOLSERS ?     */
                                    /*                               */
    POS2 = POS1+6                   /* POSITION ON DATA              */
    DO I=1 TO 3                     /* ONLY CLEAR 3 VOLSER FIELDS    */
      VOLSER.I = SUBSTR(' ',1,6)
    END
    DO I = 1 TO NUMVOL              /* MOVE VOLSERS TO OUTPUT FIELDS */
      VOLSER.I = SUBSTR(DWORK,POS2,6)
      POS2 = POS2 + 6
    END
    IF DNAMET \= DNAME THEN         /* IF RESUME, NAME MAY ALREADY   */
                                    /*    PRINTED                    */
    DO                              /* IF NOT, PRINT IT              */
     SAY COPIES(' ',8) DTYPE DNAME VOLSER.1 VOLSER.2 VOLSER.3
     DNAMET=DNAME
    END
 /********************************************************************/
 /*                                                                  */
 /*   GET POSITION OF NEXT ENTRY                                     */
 /*                                                                  */
 /********************************************************************/
    POS1 = POS1 + C2D(SUBSTR(DWORK,POS1,2))
     End
   End
 End
End
